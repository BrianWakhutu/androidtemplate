apply plugin: 'com.android.application'

ext.vMajor = 1
ext.vMinor = 5
ext.vBugfix = 0

static def evalVersionCode() {
    def process = "git rev-list --all --count".execute()
    return process.text.toInteger()
}

android {
    compileSdkVersion 27
    buildToolsVersion "27.0.3"

    signingConfigs {
        debug {
            storeFile file("shredder-debug.keystore")
            keyAlias 'followmedebug'
            keyPassword 'password'
            storePassword 'password'
        }
        release {
            keyAlias project.hasProperty('keyAlias') ? project['keyAlias'] : "false"
            keyPassword project.hasProperty('keyPassword') ? project['keyPassword'] : "false"
            storeFile file(project.hasProperty('storeFile') ? project['storeFile'] : "shredder-debug.keystore")
            storePassword project.hasProperty('storePassword') ? project['storePassword'] : "false"
        }
    }

    defaultConfig {
        applicationId "com.shredder.shellproject"
        minSdkVersion 15
        targetSdkVersion 27
        versionCode evalVersionCode()
        versionName "${vMajor}.${vMinor}.${vBugfix}.${evalVersionCode()}"
    }

    buildTypes {
        debug {
            debuggable true
            signingConfig signingConfigs.debug
            applicationIdSuffix ".debug"
            versionNameSuffix '-debug'
        }
        alpha {
            debuggable true
            signingConfig signingConfigs.debug
            applicationIdSuffix ".alpha"
            versionNameSuffix '-alpha'
        }
        beta {
            debuggable true
            signingConfig signingConfigs.debug
            applicationIdSuffix ".beta"
            versionNameSuffix '-beta'
        }
        release {
            debuggable false
            signingConfig signingConfigs.release
        }
    }
}

dependencies {
    testImplementation 'junit:junit:4.12'

    implementation fileTree(dir: 'libs', include: ['*.jar'])
    implementation 'com.android.support:appcompat-v7:27.0.2'
    implementation 'com.android.support:design:27.0.2'
    implementation 'net.hockeyapp.android:HockeySDK:5.0.3'

    implementation 'com.jakewharton:butterknife:8.8.1'
    annotationProcessor 'com.jakewharton:butterknife-compiler:8.8.1'

}